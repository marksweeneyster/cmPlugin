model {
    platforms {
        ninja(Platform) {
            generator = 'Ninja'
            cmListsPath = './../../'
        }
        nmake(Platform) {
            generator = 'NMake Makefiles'
            cmListsPath = './../../'
        }
    }
}

class CmPlugin extends RuleSource {   
    @Model
    void platforms(ModelMap<Platform> platforms) { }
     
    @Mutate
    void createPrintArgsTask(ModelMap<Task> tasks, ModelMap<Platform> printargs) {
        printargs.keySet().each { name -> 
            tasks.create("${name}Platform", PrintArgs) {
                platform = printargs[name]
            }
        }
    }
    @Mutate
    void createCmGenerateTask(ModelMap<Task> tasks, ModelMap<Platform> generators) {
        generators.keySet().each { name -> 
            tasks.create("${name}Generate", CmGenerateTask) {
                platform = generators[name]
            }
        }
    }
    @Mutate
    void createPrebuildTask(ModelMap<Task> tasks, ModelMap<Platform> prebuilders) {
        prebuilders.keySet().each { name -> 
            tasks.create("${name}PreBuild", prebuildTask) {
                platform = prebuilders[name]
            }
        }
    }
    @Mutate
    void createBuildTask(ModelMap<Task> tasks, ModelMap<Platform> builders) {
        builders.keySet().each { name -> 
            tasks.create("${name}Build", CmBuildTask) {
                platform = builders[name]
            }
        }
    }
}

@Managed
interface Platform {
    String getGenerator()
    void setGenerator(String generator)
    String getCmListsPath()
    void setCmListsPath(String cmListsPath)
}

class PrintArgs extends DefaultTask {
    Platform platform
    
    @TaskAction
    void printCommand() {
        println "cmake -G$platform.generator $platform.cmListsPath"
    }
}

class prebuildTask extends DefaultTask {
    Platform platform
    
    @TaskAction
    void makeDirectory() {
	    new File("build/$platform.generator").mkdirs()  
        //mkdir "build/$platform.generator"
    }
}

class CmGenerateTask extends Exec {
    Platform platform
    
	CmGenerateTask() {
        executable 'cmake'
    }
	
    @Override
    protected void exec() {
	
		workingDir = "build/$platform.generator"
        args = ["-G$platform.generator", "$platform.cmListsPath"]
        super.exec()
    }
}

class CmBuildTask extends Exec {
    Platform platform
    
	CmBuildTask() {
        executable 'cmake'
    }
	
    @Override
    protected void exec() {
		workingDir = "build/$platform.generator"
        args = ['--build','.']
        super.exec()
    }
}

apply plugin: CmPlugin